plugins {
    id 'java'
    id 'idea'
    id 'com.google.protobuf' version '0.8.18'
}

group = 'org.grpcdemo'
version = '1.0-SNAPSHOT'


repositories {
    maven { // The google mirror is less flaky than mavenCentral()
        url "https://maven-central.storage-download.googleapis.com/maven2/"
    }
    mavenCentral()
    mavenLocal()
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:3.21.7"
    }
    plugins {
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.49.2'
        }
    }
    generateProtoTasks {
        all() *.plugins {
            grpc {}
        }
    }
}

dependencies {
    runtimeOnly 'io.grpc:grpc-netty-shaded:1.49.2'
    implementation 'io.grpc:grpc-protobuf:1.49.2'
    implementation 'io.grpc:grpc-stub:1.49.2'
    compileOnly 'org.apache.tomcat:annotations-api:6.0.53' // necessary for Java 9+

    implementation 'com.github.javafaker:javafaker:0.12'
    implementation 'com.google.code.gson:gson:2.8.9'
    implementation 'org.apache.commons:commons-lang3:3.12.0'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
}

sourceSets {
    main {
        java {
            srcDirs 'src/main/java'
            srcDirs "$buildDir/generated"
        }
    }
}

test {
    useJUnitPlatform()
}